plugins {
	id "dev.architectury.loom" version "0.12.0-SNAPSHOT"
	id "io.github.juuxel.loom-quiltflower" version "1.7.2"
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 17

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
	silentMojangMappingsLicense()

	forge {
		mixinConfigs = [
		]

		dataGen {
			mod project.mod_id
		}

		launches {
			data {
				arg "--existing", file("src/main/resources").absolutePath
			}
		}
	}
}

repositories {
	maven {
		url 'https://maven.architectury.dev/'
		content {
			includeGroup 'dev.architectury'
			includeGroup 'me.shedaniel'
			includeGroup 'me.shedaniel.cloth'
		}
	}

	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.ftb.mods"
		}
	}

	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}

	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.officialMojangMappings()

	forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"


	//modCompileOnly modLocalRuntime("dev.architectury:architectury-forge:${project.architectury_version}")
	//modCompileOnly modLocalRuntime("me.shedaniel:RoughlyEnoughItems-forge:${project.rei_version}")

	// modRuntime("curse.maven:ctm-267602:3586556")
	//modRuntime("curse.maven:pipez-443900:3833009")
}

processResources {
	def toReplace = [
			"version"      : project.version,
			"forge_version": project.forge_version
	]

	inputs.properties toReplace
	filesMatching("META-INF/mods.toml") {
		expand toReplace
	}
}

compileJava {
	options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}

	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			from components.java
		}
	}

	repositories {
		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = project.artifact_type
			addGameVersion project.minecraft_version
			mainArtifact(remapJar.archivePath)
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}